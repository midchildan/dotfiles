zmodload zsh/system
autoload -Uz vcs_info

zstyle ':vcs_info:*+no-vcs:*' hooks async_vcs_info_no_vcs
zstyle -e ':vcs_info:*' check-for-changes \
  '[[ -n ${(M)funcstack:#async_vcs_info_worker} ]] && reply=( true ) || reply=( false )'

+vi-async_vcs_info_no_vcs() {
  typeset -g _ASYNC_VCS_INFO_NO_VCS=1
}

async_vcs_info_worker() {
  print "$sysparams[pid]"

  vcs_info

  local max_exports
  if ! zstyle -s ':vcs_info:*' max-exports max_exports; then
    max_exports=2
  fi

  local i
  for (( i = 0; i < max_exports; i++ )); do
    print -- ${(P)${:-vcs_info_msg_${i}_}}
  done
}

async_vcs_info_callback() {
  if [[ -z "$_ASYNC_VCS_INFO_FD" ]]; then
    return
  fi

  zle -F "$_ASYNC_VCS_INFO_FD"

  if [[ -n ${(Mk)parameters:#vcs_info_msg_<->_} ]]; then
    unset ${parameters[(I)vcs_info_msg_<->_]}
  fi

  local max_exports
  if ! zstyle -s ':vcs_info:*' max-exports max_exports; then
    max_exports=2
  fi

  local i
  for (( i=0; i < max_exports; i++ )); do
    typeset -g vcs_info_msg_${i}_
    read vcs_info_msg_${i}_ <&$_ASYNC_VCS_INFO_FD
  done

  exec {_ASYNC_VCS_INFO_FD}<&-
  unset _ASYNC_VCS_INFO_FD _ASYNC_VCS_WORKER_PID

  if [[ -n $_ASYNC_VCS_INFO_CALLBACK ]]; then
    "$_ASYNC_VCS_INFO_CALLBACK"
  fi
  unset _ASYNC_VCS_INFO_CALLBACK
}

async_vcs_info() {
  typeset -g _ASYNC_VCS_INFO_FD _ASYNC_VCS_WORKER_PID
  unset _ASYNC_VCS_INFO_NO_VCS

  if [[ -n $_ASYNC_VCS_INFO_FD ]]; then
    zle -F "$_ASYNC_VCS_INFO_FD"
    exec {_ASYNC_VCS_INFO_FD}<&-
  fi

  if [[ -n $_ASYNC_VCS_WORKER_PID ]]; then
		if [[ -o MONITOR ]]; then
			kill -TERM -"$_ASYNC_VCS_WORKER_PID" 2>/dev/null
		else
			kill -TERM "$_ASYNC_VCS_WORKER_PID" 2>/dev/null
		fi
  fi

  vcs_info
  if [[ -n $_ASYNC_VCS_INFO_NO_VCS ]]; then
    unset _ASYNC_VCS_INFO_FD _ASYNC_VCS_WORKER_PID _ASYNC_VCS_INFO_NO_VCS
    return
  fi

  exec {_ASYNC_VCS_INFO_FD}< <(async_vcs_info_worker)
  read _ASYNC_VCS_WORKER_PID <&$_ASYNC_VCS_INFO_FD

  typeset -g _ASYNC_VCS_INFO_CALLBACK="$1"
  zle -F "$_ASYNC_VCS_INFO_FD" async_vcs_info_callback
}

async_vcs_info "$@"

# vim:set et sw=2 ft=zsh:
