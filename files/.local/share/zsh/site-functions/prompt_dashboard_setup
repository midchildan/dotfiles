# dashboard prompt theme inspired by denysdovhan/spaceship-prompt

prompt_dashboard_setup() {
  zmodload zsh/parameter

  autoload -Uz async_vcs_info
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' max-exports 4
  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' unstagedstr '!'
  zstyle ':vcs_info:*' formats '%r/%S' '%b' '%u%c'
  zstyle ':vcs_info:*' actionformats '%r/%S' '%b' '%u%c' '%a'

  local container
  if ! zstyle -s ':dotfiles:prompt:dashboard:container' type container; then
    if [[ -f /.dockerenv ]]; then
      zstyle ':dotfiles:prompt:*:container' type docker
    elif [[ -f /run/.containerenv ]]; then
      zstyle ':dotfiles:prompt:*:container' type podman
    elif [[ -r /run/host/container-manager ]]; then
      read -r container < /run/host/container-manager
      zstyle ':dotfiles:prompt:*:container' type "$container"
    fi
  fi

  prompt_opts=(cr percent sp)
  add-zsh-hook precmd prompt_dashboard_precmd
  add-zsh-hook preexec prompt_dashboard_preexec
  unset RPROMPT
}

prompt_dashboard_set_prompt() {
  local prompt_mark='%#' pbegin pend vcs_icon nix_icon
  if zstyle -T ':dotfiles:iterm2:osc' enable; then
    pbegin=$'%{\e]133;D;%?\a\e]133;A\a%}'
    pend=$'%{\e]133;B\a%}'
  fi
  if [[ "$USE_POWERLINE" == 1 ]]; then
    vcs_icon=' '
    nix_icon='❆ '
    prompt_mark='❯'
  fi

  PROMPT="$pbegin"

  if (( EUID == 0 || UID == 0 || EUID != UID )); then
    PROMPT+="%B%F{red}%n%f%b in "
  fi

  vcs_info_msg_0_="${vcs_info_msg_0_%/.}"
  PROMPT+="%B%F{blue}${vcs_info_msg_0_:-%~}%f%b"

  if [[ -n $SSH_CONNECTION$SSH_TTY ]]; then
    PROMPT+=" at %B%F{yellow}%m%f%b"
  fi
  if [[ -n $vcs_info_msg_1_ ]]; then
    PROMPT+=" on %B%F{magenta}$vcs_icon$vcs_info_msg_1_%f%b"
  fi
  if [[ -n $vcs_info_msg_2_ ]]; then
    PROMPT+=" %B%F{red}[$vcs_info_msg_2_]%f%b"
  elif [[ -n $vcs_info_msg_0_ ]]; then
    PROMPT+=" %B%F{red}[?]%f%b"
  fi
  if [[ -n $vcs_info_msg_3_ ]]; then
    PROMPT+=" doing %B%F{green}$vcs_info_msg_3_%f%b"
  fi
  if [[ -n $IN_NIX_SHELL ]]; then
    PROMPT+=" via %B%F{cyan}$nix_icon$name%f%b"
  fi

  local container
  if zstyle -s ':dotfiles:prompt:dashboard:container' type container; then
    PROMPT+=" inside %B$container%b"
  fi

  PROMPT+=$'\n'"%B%(?:%F{green}:%F{red})$prompt_mark%f%b $pend"
}

prompt_dashboard_async_handler() {
  prompt_dashboard_set_prompt
  zle reset-prompt
}

prompt_dashboard_precmd() {
  async_vcs_info prompt_dashboard_async_handler
  prompt_dashboard_set_prompt
}

prompt_dashboard_preexec() {
  if zstyle -T ':dotfiles:iterm2:osc' enable; then
    print -Pn "\e]133;C\a"
  fi
}

prompt_dashboard_setup "$@"

# vim:set et sw=2 ft=zsh:
