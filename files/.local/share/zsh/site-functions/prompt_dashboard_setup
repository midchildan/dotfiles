# dashboard prompt theme inspired by denysdovhan/spaceship-prompt

prompt_dashboard_setup() {
  zmodload zsh/parameter

  autoload -Uz async_vcs_info
  zstyle ':vcs_info:*' max-exports 4
  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' unstagedstr '!'
  zstyle ':vcs_info:*' untrackedstr '*'
  zstyle ':vcs_info:*' stashedstr '≡%d'
  zstyle ':vcs_info:*' aheadstr '↑%d'
  zstyle ':vcs_info:*' behindstr '↓%d'
  zstyle ':vcs_info:*' formats '%r/%S' '%b' '%?%u%c%+%_%='
  zstyle ':vcs_info:*' actionformats '%r/%S' '%b' '%?%u%c%+%_%=' '%a'

  local -r prefix=':dotfiles:prompt:dashboard:section'
  local style

  style="${prefix}:username"
  zstyle "$style" preposition as
  zstyle "$style" format '%n'
  zstyle -e "$style" color \
    '(( EUID == 0 || UID == 0 )) && reply=(red) || reply=(green)'
  zstyle -e "$style" show \
    '(( EUID == 0 || UID == 0 || EUID != UID )) && reply=(true) || reply=(false)'

  style="${prefix}:directory"
  zstyle "$style" preposition in
  zstyle -e "$style" format 'reply=("${${vcs_info_msg_0_%/.}:-%~}")'
  zstyle "$style" color blue

  style="${prefix}:hostname"
  zstyle "$style" preposition at
  zstyle "$style" format '%m'
  zstyle "$style" color yellow
  zstyle -e "$style" show \
    '[[ -n $SSH_CONNECTION$SSH_TTY ]] && reply=(true) || reply=(false)'

  style="${prefix}:vcs-branch"
  zstyle "$style" preposition on
  zstyle "$style" icon ''
  zstyle -e "$style" format 'reply=("$vcs_info_msg_1_")'
  zstyle "$style" color magenta

  style="${prefix}:vcs-status"
  zstyle -e "$style" format 'reply=("[${(*)vcs_info_msg_2_/\?\?##/?}]")'
  zstyle "$style" color red
  zstyle -e "$style" show \
    '[[ -n $vcs_info_msg_2_ ]] && reply=(true) || reply=(false)'

  style="${prefix}:vcs-action"
  zstyle "$style" preposition doing
  zstyle -e "$style" format 'reply=("$vcs_info_msg_3_")'
  zstyle "$style" color green

  style="${prefix}:nix-shell"
  zstyle "$style" preposition via
  zstyle "$style" icon '❆'
  zstyle -e "$style" format 'reply=("$name")'
  zstyle "$style" color cyan
  zstyle -e "$style" show \
    '[[ -n $IN_NIX_SHELL ]] && reply=(true) || reply=(false)'

  style="${prefix}:container"
  zstyle "$style" preposition inside
  if [[ -f /.dockerenv ]]; then
    zstyle "$style" format docker
  elif [[ -f /run/.containerenv ]]; then
    zstyle "$style" format podman
  elif [[ -r /run/host/container-manager ]]; then
    local container
    read -r container < /run/host/container-manager
    zstyle "$style" format "$container"
  fi

  prompt_opts=(cr percent sp)
  add-zsh-hook precmd prompt_dashboard_precmd
  add-zsh-hook preexec prompt_dashboard_preexec
  unset RPROMPT
}

prompt_dashboard_set_prompt() {
  local symbol start fin
  if zstyle -T ':dotfiles:prompt:dashboard' enable-semantic-markers; then
    start=$'%{\e]133;D;%?\a\e]133;A\a%}'
    fin=$'%{\e]133;B\a%}'
  fi

  if ! zstyle -s ':dotfiles:prompt:dashboard' prompt symbol; then
    if zstyle -T ':dotfiles:prompt:dashboard' show-icons; then
      symbol='❯'
    else
      symbol='%#'
    fi
  fi

  local -a sections
  if ! zstyle -a ':dotfiles:prompt:dashboard' sections sections; then
    sections=(
      username
      directory
      hostname
      vcs-{branch,status,action}
      nix-shell
      container
    )
  fi

  local section style text format preposition color icon
  for section in "${sections[@]}"; do
    style=":dotfiles:prompt:dashboard:section:$section"

    if ! zstyle -T "$style" show; then
      continue
    fi

    zstyle -s "$style" format format
    if [[ -z $format ]]; then
      continue
    fi

    if [[ -n "$text" ]]; then
      zstyle -s "$style" preposition preposition
      text+=" $preposition${preposition:+ }"
    fi

    zstyle -s "$style" color color
    if zstyle -T ':dotfiles:prompt:dashboard' show-icons; then
      zstyle -s "$style" icon icon
    fi

    text+="%B%F{${color:-default}}$icon${icon:+ }$format%f%b"
  done

  PROMPT="$start$text"$'\n'"%B%(?:%F{green}:%F{red})$symbol%f%b $fin"
}

prompt_dashboard_async_handler() {
  prompt_dashboard_set_prompt
  zle reset-prompt
}

prompt_dashboard_precmd() {
  async_vcs_info prompt_dashboard_async_handler
  prompt_dashboard_set_prompt
}

prompt_dashboard_preexec() {
  if zstyle -T ':dotfiles:prompt:dashboard' enable-semantic-markers; then
    print -Pn "\e]133;C\a"
  fi
}

prompt_dashboard_setup "$@"

# vim:set et sw=2 ft=zsh:
