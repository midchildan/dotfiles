emulate -L zsh

autoload -Uz fzf-widgets && fzf-widgets init || return 1

fzf-cdr-widget() {
  emulate -L zsh
  setopt localoptions pipefail no_aliases 2> /dev/null

  local -a reply
  autoload -Uz chpwd_recent_filehandler && chpwd_recent_filehandler $reply

  local fzfopts="--height ${FZF_TMUX_HEIGHT:-40%} --reverse $FZF_DEFAULT_OPTS"
  local dir="$(for line in $reply; do echo "$line"; done \
    | FZF_DEFAULT_OPTS="$fzfopts" $(__fzfcmd) +m)"
  if [[ -z "$dir" ]]; then
    zle redisplay
    return 0
  elif [[ ! -d "$dir" ]]; then
    autoload -Uz cdr && { cdr -P "$dir"; cdr -P "$dir/**" }
    zle redisplay
    zle -M "No such directory: $dir"
    return 0
  fi

  zle push-line
  BUFFER="cd ${(q)dir}"
  zle accept-line
  local ret=$?

  unset dir
  zle reset-prompt

  return $ret
}

fzf-cdr-widget
