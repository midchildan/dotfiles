emulate -L zsh
zmodload -i zsh/parameter

local fzfscripts
if (( $+commands[fzf-share] )); then
  fzfscripts=( "$(fzf-share)/key-bindings.zsh" )
else
  fzfscripts=(
    ~/.vim/plugged/fzf/shell/key-bindings.zsh
    /usr/local/opt/fzf/shell/key-bindings.zsh
    /usr/share/doc/fzf/examples/key-bindings.zsh
    ~/.local/opt/fzf/shell/key-bindings.zsh
  )
fi

local file
for file in "$fzfscripts[@]"; do
  if [[ -f "$file" ]]; then
    FZF_CTRL_R_OPTS="${FZF_CTRL_R_OPTS:---reverse}"

    {
      function bindkey() {} # don't let fzf change my keybindings
      source "$file"
    } always {
      unfunction bindkey
    }

    function fzf-widgets() {} # guard against multiple invocations
    return
  fi
done

return 1
