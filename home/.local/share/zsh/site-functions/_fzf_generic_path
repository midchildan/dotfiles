# vim:set et sw=2 ts=8 ft=zsh:

local base=${(Q)1}
local lbuf=$2
local compgen=$3
local fzf_opts=$4
local suffix=$5
local tail=$6

setopt localoptions nonomatch
local dir="$base"
while true; do
  if [[ -z "$dir" || -d ${~dir} ]]; then
    local leftover=${base/#"$dir"}
    leftover=${leftover/#\/}
    [[ -z "$dir" ]] && dir='.'
    [[ "$dir" != "/" ]] && dir="${dir/%\//}"
    dir=${~dir}
    local matches=$(
      eval "$compgen $(printf %q "$dir")" \
        | fzf --height 50% --reverse ${=fzf_opts} -q "$leftover" \
        | while read item; do; echo -n "${(q)item}$suffix "; done)
    matches=${matches% }
    if [ -n "$matches" ]; then
      LBUFFER="$lbuf$matches$tail"
    fi
    zle redisplay
    typeset -f zle-line-init >/dev/null && zle zle-line-init
    break
  fi
  dir=$(dirname "$dir")
  dir=${dir%/}/
done
