emulate -L zsh

local strp
if command -v perl > /dev/null 2>&1; then
  strp="$(
    echo "$BUFFER" \
      | perl -n0777e \
          'printf "%d:%s\n", pos()-length($&), $& while /\{\{[^{}]*\}\}/g' \
      | head -1
  )"
else
  # macos version of grep seems to have a bug that always prints 0 as the offset
  strp="$(echo "$BUFFER" | grep -b -o '{{[^{}]*}}' | head -1)"
fi
if [[ -z "$strp" ]]; then
  if [[ $WIDGET = fzf-snippet-next-or-accept-line ]]; then
    zle accept-line
  else
    zle .self-insert
  fi
  return "$?"
fi

local pos="$(echo "$strp" | cut -d':' -f1)"
local placeholder="$(echo "$strp" | cut -d':' -f2)"

if [[ -n "$1" ]]; then
  BUFFER="$(echo "$BUFFER" | sed -e 's/{{//' -e 's/}}//')"
  CURSOR=$(( $pos + ${#placeholder} -4 ))
else
  BUFFER="$(echo "$BUFFER" | sed "s/${placeholder:gs@/@\\/}//")"
  CURSOR="$pos"
fi
local ret="$?"

zle redisplay
typeset -f zle-line-init >/dev/null && zle zle-line-init
return "$ret"
