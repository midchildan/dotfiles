# vim:set et sw=2 ts=8 ft=zsh:

if ! (command -v fzf > /dev/null 2>&1) || [[ "$TERM" = "dumb" ]]; then
  if ! (zle -al complete-file); then
    zle -C complete-file complete-word _generic &&
      zstyle ':completion:complete-file:*' completer _files
  fi
  zle complete-file
  return
fi

local cmd="command find -L .  \\( -path '*/\\.*' \
  -o -fstype 'devfs' -o -fstype 'devtmpfs' -o -fstype 'proc' -o -fstype 'sysfs' \
  \\) -prune -o -type f -print -o -type d -print -o -type l -print \
  2> /dev/null | cut -b3-"

setopt localoptions pipefail 2> /dev/null
eval "$cmd | fzf --height 50% --reverse -m" | while read item; do
  LBUFFER+="$(echo -n "${(q)item} ")"
done
local ret=$?

zle redisplay
typeset -f zle-line-init >/dev/null && zle zle-line-init
return $ret
